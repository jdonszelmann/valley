

name = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
integer = @{ "_"? ~ ASCII_DIGIT+ }
string = @{PUSH("\"" | "\'") ~ (!PEEK ~ ANY | escape)* ~ POP}

escape = {
      "\\" ~ ("\"" | "\\" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

expr_type = {
      "u8"
    | "u16"
    | "*" ~ expr_type
}

atom = {
      integer
    | string
    | block_expr
    | name
}

primary = {
      atom
    | "(" ~ expr ~ ")"
}

funccall = {
      primary ~ "(" ~ expr? ~ ("," ~ expr)* ~ ")"
    | primary
}

power_op = {"**" | "^"}
power = {
      funccall ~ power_op ~ factor
    | funccall
}

factor_op = {"++" | "--"}
factor = {
      factor_op ~ factor
    | power
}

term_op = {"*" | "/" | "%"}
term = {factor ~ (term_op ~  factor)*}
sum_op = {"+" | "-"}
sum = {term ~ ( sum_op ~ term )*}

expr = {sum}

assignment = {name ~ "=" ~ expr}
let_assignment = {"let" ~ name ~ ":" ~ expr_type ~ "=" ~ expr}

line = {
      assignment
    | let_assignment
    | expr
    | block_expr
}

block_expr = {
    funcdef
}

param = {
    name ~ ":" ~ expr_type
}

funcdef = {
    "fn" ~ name ~ "(" ~ param? ~ ("," ~ param)* ~ ")" ~ block
}

block = {"{" ~ inner_program ~ "}"}
inner_program = _{(line ~ ";"+) * ~ line? ~ ";"?}

toplevel = _{
    funcdef
}

toplevel_program = _{toplevel*}

program = _{ SOI ~ toplevel_program ~ EOI }


WHITESPACE = _{ " " | "\t" | "\n" }
COMMENT = _{ "#" ~ (!"\n" ~ ANY)* }